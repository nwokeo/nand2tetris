// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1


CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    //zx
        Mux16(a=x, b=false, sel=zx, out=xout);

    //nx
        Not16(in=xout, out=notxout);
        Mux16(a=xout, b=notxout, sel=nx, out=xout1);
        
    //zy
        Mux16(a=y, b=false, sel=zy, out=yout);

    //ny
        Not16(in=yout, out=notyout);
        Mux16(a=yout, b=notyout, sel=ny, out=yout1);
        
    //f
        //and
        And16(a=xout1, b=yout1, out=andout);
            
        //add
        Add16(a=xout1, b=yout1, out=addout);
            
        Mux16(a=andout, b=addout, sel=f, out=nout);
        
    //no
        Not16(in=nout, out=noout);
        Mux16(a=nout, b=noout, sel=no, out=out);
        
        Mux16(a=nout, b=noout, sel=no, out[0..7]=out2); //feed to zr
        Mux16(a=nout, b=noout, sel=no, out[8..15]=out3); //feed to zr
        Mux16(a=nout, b=noout, sel=no, out[15]=out4); //feed to ng
                
    //zr
        Or8Way(in=out2, out=zrout1);
        Or8Way(in=out3, out=zrout2);
        Or(a=zrout1, b=zrout2, out=zrout3);
        Not(in=zrout3, out=zr);
    
    //ng
        And(a=out4, b=out4, out=ng);
}